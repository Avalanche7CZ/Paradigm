plugins {
    id 'java-library'
    id 'maven-publish'
    id 'idea'
    id 'net.neoforged.moddev' version '2.0.116'
}



version = project.mod_version
group = project.maven_group

def archivesBaseName = project.hasProperty('archives_base_name') ? project.archives_base_name : (project.hasProperty('mod_id') ? project.mod_id : project.name)
def modId = project.hasProperty('mod_id') ? project.mod_id : archivesBaseName

def modName = project.hasProperty('mod_name') ? project.mod_name : archivesBaseName

base {
    archivesName = archivesBaseName
}

repositories {
    mavenLocal()
    maven { url "https://maven.neoforged.net/releases/" }
    maven { url "https://maven.neoforged.net/" }
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.terraformersmc.com/releases/" }
    mavenCentral()
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
}

neoForge {
    version = project.neo_version

    parchment {
        mappingsVersion = project.hasProperty('yarn_mappings') ? project.yarn_mappings : project.parchment_mappings_version
        minecraftVersion = project.minecraft_version
    }

    runs {
        client {
            client()
            systemProperty 'neoforge.enabledGameTestNamespaces', archivesBaseName
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', archivesBaseName
        }

        gameTestServer {
            type = "gameTestServer"
            systemProperty 'neoforge.enabledGameTestNamespaces', archivesBaseName
        }

        data {
            data()
            programArguments.addAll '--mod', archivesBaseName, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        "${archivesBaseName}" {
            sourceSet(sourceSets.main)
        }
    }
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

dependencies {
    compileOnly 'net.luckperms:api:5.4'
}

jar {

    manifest {
        attributes([
            "Specification-Title": "Paradigm",
            "Specification-Vendor": "Avalanche7CZ",
            "Specification-Version": project.version,
            "Implementation-Title": project.name,
            "Implementation-Version": "${project.mod_version}",
            "Implementation-Vendor": "Avalanche7CZ",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [
            minecraft_version      : project.minecraft_version,
            minecraft_version_range: project.hasProperty('minecraft_version_range') ? project.minecraft_version_range : project.minecraft_version,
            neo_version            : project.hasProperty('neo_version') ? project.neo_version : project.minecraft_version,
            neo_version_range      : project.hasProperty('neo_version_range') ? project.neo_version_range : project.minecraft_version,
            loader_version_range   : project.hasProperty('loader_version_range') ? project.loader_version_range : "",
            mod_id                 : modId,
            mod_name               : modName,
            mod_license            : project.hasProperty('mod_license') ? project.mod_license : "MIT",
            mod_version            : project.mod_version,
            mod_authors            : project.hasProperty('mod_authors') ? project.mod_authors : "",
            mod_description        : project.hasProperty('mod_description') ? project.mod_description : ""
    ]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}

sourceSets.main.resources.srcDir generateModMetadata
neoForge.ideSyncTask generateModMetadata

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
