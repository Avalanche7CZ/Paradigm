// Common Gradle configuration for all Fabric versions

def getGitCommitHash = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (Exception e) {
        return "unknown"
    }
}

repositories {
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.terraformersmc.com/releases/" }
    mavenCentral()
}

loom {
    runs {
        server {
            runDir = "run"
        }
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.compilerArgs.addAll(['-Xlint:deprecation', '-Xlint:unchecked'])
}

java {
    withSourcesJar()
}

jar {
    from("../LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }

    manifest {
        attributes([
            "Specification-Title": "Paradigm",
            "Specification-Vendor": "Avalanche7CZ",
            "Specification-Version": project.version,
            "Implementation-Title": project.name,
            "Implementation-Version": "${project.mod_version}-${getGitCommitHash()}",
            "Implementation-Vendor": "Avalanche7CZ",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    repositories {
    }
}

